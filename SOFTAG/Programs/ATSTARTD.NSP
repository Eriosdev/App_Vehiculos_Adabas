* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
************************************************************************
*                                                                      *
* https://documentation.softwareag.com/naturalONE/natONE911/natwin/sm/atstart.htm
*                                                                      *
*                         AT START OF DATA                             *
************************************************************************
*
DEFINE DATA LOCAL
*
1 EMPLOY-VIEW VIEW OF EMPLOYEES
  2 NAME
  2 FIRST-NAME
  2 CITY
*
1 #CNTL (A1) INIT <' '>
1 #CITY (A20) INIT <' '>
*
END-DEFINE
*
REPEAT
* Solicita ingresar el nombre de la ciudad en #CITY
  INPUT 'Ingrese ciudad o ''end'' para salir' #CITY
*
* Formateo #CITY
  EXAMINE #CITY TRANSLATE INTO UPPER
  MOVE *TRIM(#CITY) TO #CITY
*  
* Si #CITY es (' ') o 'END', detiene el programa
  IF #CITY = ' ' OR = 'END'
    STOP
  END-IF
*
  FIND EMPLOY-VIEW WITH CITY = #CITY
*
    IF NO RECORDS FOUND
      WRITE NOTITLE NOHDR 'NO RECORDS FOUND'
      ESCAPE BOTTOM
    END-NOREC
*
    AT START OF DATA
* Solicita al usuario ingresar 'D' para mostrar los registros en #CNTL
      INPUT (AD=O) 'Registros encontrados:' *NUMBER //
        '''D'' para verlos' #CNTL (AD=A)
      EXAMINE #CNTL TRANSLATE INTO UPPER
      /* Si #CNTL no es igual a 'D', sale del programa
      IF #CNTL NE 'D'
        ESCAPE BOTTOM
      END-IF
    END-START
*
*   Muestra los valores de CITY, NAME y FIRST-NAME
    DISPLAY NOTITLE CITY NAME FIRST-NAME
  END-FIND
*
END-REPEAT
*
END

